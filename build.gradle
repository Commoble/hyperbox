
buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven {url='https://repo.spongepowered.org/repository/maven-public/'}
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

version = "${mod_version}"
group = "commoble.${modid}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${modid}-${mc_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
jarJar.enable()

minecraft {
    mappings channel: 'official', version: "${mc_version}"    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=hyperbox.mixins.json"

            mods {
                hyperbox {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=hyperbox.mixins.json"

            mods {
                hyperbox {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=hyperbox.mixins.json"

            args '--mod', 'hyperbox', '--all', '--output', file('src/generated/resources/')

            mods {
                hyperbox {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
	mavenCentral()
	maven {url = "https://maven.blamejared.com/"}
	maven {url = "https://dvs1.progwml6.com/files/maven/"}
	maven {url = "https://modmaven.dev"}
	maven {url = "https://cursemaven.com"}
	maven {url = "https://cubicinterpolation.net/maven/"}
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    
	// compile against the JEI API but do not include it at runtime
	// compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
	// at runtime, use the full JEI jar
	// runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
	implementation fg.deobf("commoble.infiniverse:${infiniverse_mc_version}:${infiniverse_version}")
	jarJar(group: "commoble:infiniverse", name: "${infiniverse_mc_version}", version: "[${infiniverse_min_version}, ${infiniverse_max_version})")
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
	classifier 'slim'
    manifest {
        attributes([
            "Specification-Title": "${modid}",
            "Specification-Vendor": "Commoble",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Commoble",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "${modid}.mixins.json"
        ])
    }
}


reobf {
    jarJar { }
}

// this replaces jar.finalizedBy('reobfJar') in the standard forge mod buildscript
tasks.build.dependsOn tasks.jarJar
jar.finalizedBy('reobfJarJar')

// run "gradlew build publish" to build jars and generate a maven repo for them
// run "gradlew build" to build the jars without mavening them
publishing {
    publications {
        mavenJava(MavenPublication) {
			artifactId project.archivesBaseName
            artifact tasks.jarJar
			//artifact sourcesJar
			//artifact javadocJar
        }
    }
    repositories {
        maven {
			name="localMaven"
            url "file:///${project.projectDir}/maven"
        }
    }
}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}
